# ================================================================================
# Jigoku Shoujo Mioyosuga (PS2)
# PID / PTD extract
# QuickBMS script by DKDave, 2023
# ================================================================================

# Notes:
# Audio files will have a VAG header added so that they can be played directly
# Music files are split into separate mono files for each channel, so will need to be combined separately
# Files are split into separate folders for each PTD archive


Get ARC_COUNT Long
Math ENTRY = 4
XMath ARC_NAMES "(ARC_COUNT * 4) + 4"
XMath ENTRY2 "ARC_NAMES + (ARC_COUNT * 0x10)"

Goto ARC_NAMES

For A = 1 To ARC_COUNT
	GetDString PTD_FILE 0x10
	Open FDSE PTD_FILE A
	String PTD_FILE - ".PTD"
	PutArray 0 A PTD_FILE
Next A

Math FNUM = 0

For A = 1 To ARC_COUNT
	Goto ENTRY
	Get COUNT1 Long
	GetArray FLDR 0 A

	For B = 0 < COUNT1
		Goto ENTRY2
		Get OFFSET Long
		Math OFFSET * 0x800
		Get SIZE Long

		If SIZE > 0
			If A = 2 Or A > 4								# Audio archives
				Endian Big
				Log MEMORY_FILE 0 0
				PutVarChr MEMORY_FILE 0x2f 0 Byte
				PutVarChr MEMORY_FILE 0 0x56414770 Long
				PutVarChr MEMORY_FILE 4 0x04 Long
				PutVarChr MEMORY_FILE 0x0c SIZE Long
				PutVarChr MEMORY_FILE 0x10 0xac44 Long
				Endian Little

				String FILENAME P "%FLDR%\JIGOKU_%FNUM%.vag"

				Append
				Log MEMORY_FILE OFFSET SIZE A
				Append

				Get MEM_SIZE asize -1
				Log FILENAME 0 MEM_SIZE -1

			Else									# Everything else
				Goto OFFSET A
				GetDString TYPE 4 A

				If TYPE = "TIM2"
					String EXT = ".tm2"
				Else
					String EXT = ""
				Endif

				String FILENAME P "%FLDR%\JIGOKU_%FNUM%%EXT%"
				Log FILENAME OFFSET SIZE A
			Endif

			Math FNUM + 1
		Endif

		Math ENTRY2 + 8
	Next B

	Math ENTRY + 4
Next A


