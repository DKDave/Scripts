# ================================================================================
# Code Lyoko: Quest For Infinity (PS2)
# MUSIC.GRP extract
# QuickBMS script by DKDave, 2023
# ================================================================================

# Files are output as .ss2 files which will play in Foobar/vgmstream


Get TEMPNAME basename
Get FILES Short
Math FILES / 2
Get JUNK Short

# Create SS2 header

Log MEMORY_FILE 0 0
PutVarChr MEMORY_FILE 0x27 0 Byte
PutVarChr MEMORY_FILE 0 0x64685353 Long					# SShd
PutVarChr MEMORY_FILE 0x04 0x18 Long
PutVarChr MEMORY_FILE 0x08 2 Long						# PS ADPCM
PutVarChr MEMORY_FILE 0x20 0x64625353 Long					# SSbd
PutVarChr MEMORY_FILE 0x18 0xFFFFFFFF Long
PutVarChr MEMORY_FILE 0x1c 0xFFFFFFFF Long

Math ENTRY1 = 4
XMath ENTRY2 "ENTRY1 + (FILES * 8)"

For A = 0 < FILES
	Goto ENTRY1
	Get JUNK Long
	Get INFO_OFF Long
	Goto ENTRY2
	Get SIZE Long
	Get OFFSET Long
	Goto INFO_OFF
	Get JUNK Long
	Get JUNK Long
	Get CHANNELS Long
	Get SAMP_RATE Long
	Get INTERLEAVE Long

	PutVarChr MEMORY_FILE 0xc SAMP_RATE Long
	PutVarChr MEMORY_FILE 0x10 CHANNELS Long

	If CHANNELS = 2
		PutVarChr MEMORY_FILE 0x14 INTERLEAVE Long
	Else
		PutVarChr MEMORY_FILE 0x14 0 Long
	Endif

	PutVarChr MEMORY_FILE 0x24 SIZE Long

	String FILENAME P "%TEMPNAME%_%A%.ss2"

	Log FILENAME 0 0x28 -1
	Append
	Log FILENAME OFFSET SIZE
	Append

	Math ENTRY1 + 8
	Math ENTRY2 + 8
Next A

