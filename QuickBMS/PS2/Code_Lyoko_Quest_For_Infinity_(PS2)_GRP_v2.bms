# ================================================================================
# Code Lyoko: Quest For Infinity (PS2)
# MUSIC.GRP audio extract
# QuickBMS script by DKDave, 2023 (v2)
# ================================================================================

# Files are extracted as .ss2 files playable in Foobar/vgmstream


Get TEMPNAME basename
Get FILES Short
Math FILES / 2
Get JUNK Short

# Create SS2 header

Log MEMORY_FILE 0 0
PutVarChr MEMORY_FILE 0x27 0 Byte
PutVarChr MEMORY_FILE 0 0x64685353 Long					# SShd
PutVarChr MEMORY_FILE 0x04 0x18 Long
PutVarChr MEMORY_FILE 0x08 2 Long						# PS ADPCM
PutVarChr MEMORY_FILE 0x20 0x64625353 Long					# SSbd
PutVarChr MEMORY_FILE 0x18 0xFFFFFFFF Long
PutVarChr MEMORY_FILE 0x1c 0xFFFFFFFF Long

Math TABLE1 = 0x04
XMath TABLE2 "TABLE1 + (FILES * 8)"

For A = 0 < FILES
	Goto TABLE1
	Get JUNK Long
	Get AUDIO_INFO_OFFSET Long
	Goto TABLE2
	Get SIZE Long
	Get OFFSET Long
	Goto AUDIO_INFO_OFFSET
	Get JUNK Long
	Get JUNK Long
	Get CHANNELS Long
	Get SAMP_RATE Long
	Get INTERLEAVE Long

	PutVarChr MEMORY_FILE 0x0c SAMP_RATE Long
	PutVarChr MEMORY_FILE 0x10 CHANNELS Long
	PutVarChr MEMORY_FILE 0x24 SIZE Long

	If CHANNELS = 1
		PutVarChr MEMORY_FILE 0x14 0 Long
	Else
		PutVarChr MEMORY_FILE 0x14 INTERLEAVE Long
	Endif

	String FILENAME P "%TEMPNAME%_%A%.ss2"

	Log FILENAME 0 0x28 -1
	Append
	Log FILENAME OFFSET SIZE
	Append

	Math TABLE1 + 8
	Math TABLE2 + 8
Next A

