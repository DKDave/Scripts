# ================================================================================
# Once Human (Beta)
# NPK archive extract
# QuickBMS script by DKDave, 2023 - updated 3 January 2024
# ================================================================================

# Should work for older NetEase games with the same archive format


IDString "NXPK"

Get FILES Long
Goto 0x14
Get ENTRY Long
XMath NAMES "ENTRY + (FILES * 0x1c) + 0x10"

For A = 0 < FILES
	Goto ENTRY
	Get MISC1 Long
	Get OFFSET Long
	Get ZSIZE Long
	Get SIZE Long
	Get MISC2 Long
	Get MISC3 Long
	Get COMP_TYPE Long
	Goto NAMES
	Get FILENAME String
	SavePos NAMES

	If ZSIZE <> SIZE								# Compressed files
		If COMP_TYPE = 2
			ComType LZ4
			CLog FILENAME OFFSET ZSIZE SIZE

		ElIf COMP_TYPE = 3
			Comtype ZSTD
			CLog FILENAME OFFSET ZSIZE SIZE
		Endif

	Else									# Uncompressed files
		If ZSIZE > 0
			Log FILENAME OFFSET ZSIZE
		Endif
	Endif

	Math ENTRY + 0x1c
Next A


