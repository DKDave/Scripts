# ================================================================================
# Ghost Of Tsushima (PC)
# PSARC extract
# QuickBMS script by DKDave, 2024
# ================================================================================


GetDString CHECK 4

If CHECK = "DSAR"								# Decompress archive if needed
	ComType LZ4

	Get TEMPNAME basename
	Goto 8
	Get FILES Long
	Math ENTRY = 0x20

	For A = 0 < FILES
		Goto ENTRY
		Get MISC1 LongLong
		Get OFFSET LongLong
		Get SIZE Long
		Get ZSIZE Long

		Append
		CLog MEMORY_FILE OFFSET ZSIZE SIZE
		Append

		Math ENTRY + 0x20
	Next A

Else
	Get TEMP asize
	Log MEMORY_FILE 0 TEMP
Endif


# Decompressed PSAR file is now in MEMORY_FILE

Endian Big

Goto 0x14 -1
Get FILES Long -1

Math ENTRY = 0x20

For A = 0 < FILES
	Goto ENTRY -1
	Goto 0x10 -1 SEEK_CUR
	Get MISC1 Long -1
	Get TEMP1 Byte -1
	Get SIZE Long -1
	XMath SIZE "SIZE + (TEMP1 * 0x100000000)"
	Get TEMP1 Byte -1
	Get OFFSET Long -1
	XMath OFFSET "OFFSET + (TEMP1 * 0x100000000)"

	If A = 0								# Filename list
		Log MEMORY_FILE2 OFFSET SIZE -1
		Math NAMES = 0
		PutVarChr MEMORY_FILE2 SIZE Byte
	Else
		Goto NAMES -2
		Get FILENAME String -2
		SavePos NAMES -2
		Log FILENAME OFFSET SIZE -1
	Endif

	Math ENTRY + 0x1e
Next A

