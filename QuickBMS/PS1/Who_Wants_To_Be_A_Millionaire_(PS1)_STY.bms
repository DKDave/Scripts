# ================================================================================
# Who Wants To Be A Millionaire (PS1)
# STY extract
# QuickBMS script by DKDave, 2022
# ================================================================================

# Files are extracted as-is from the STY archive, except for the audio in "MainAudio.ssl" which is extracted separately as .ss2 files


# Create SS2 header

Log MEMORY_FILE 0 0
PutVarChr MEMORY_FILE 0x27 0 Byte
PutVarChr MEMORY_FILE 0 0x64685353 Long						# SShd
PutVarChr MEMORY_FILE 0x04 0x18 Long
PutVarChr MEMORY_FILE 0x08 2 Long							# PS ADPCM
PutVarChr MEMORY_FILE 0x20 0x64625353 Long						# SSbd
PutVarChr MEMORY_FILE 0x18 0xFFFFFFFF Long
PutVarChr MEMORY_FILE 0x1c 0xFFFFFFFF Long


Goto 4
Get ENTRIES Long

For A = 0 < ENTRIES
	GetDString FILENAME 0x18
	Get SIZE Long
	Get OFFSET Long
	Math OFFSET * 0x800

	If FILENAME = "MainAudio.ssl"							# Extract audio files
		SavePos TEMP1
		Goto OFFSET
		Get FILES Long
		Math FILES / 8

		For B = 0 < FILES
			Get OFFSET2 Long
			Math OFFSET2 * 0x800
			Math OFFSET2 + OFFSET
			Get SIZE2 Short
			Get INFO1 Short						# bit 0 = stereo flag, bit 1 = ?
			Math SIZE2 * 0x800
			String FILENAME2 P "MainAudio\MainAudio_%B%.ss2"

			If (INFO1 & 1)
				Math CHANNELS = 2
				Math INTERLEAVE = 0x800
			Else
				Math CHANNELS = 1
				Math INTERLEAVE = 0
			Endif

			PutVarChr MEMORY_FILE 0x0c 22050 Long			# Always 22050 in this game ?
			PutVarChr MEMORY_FILE 0x10 CHANNELS Long
			PutVarChr MEMORY_FILE 0x14 INTERLEAVE Long
			PutVarChr MEMORY_FILE 0x24 SIZE2 Long

			Log MEMORY_FILE2 0 0x28 -1
			Append
			Log MEMORY_FILE2 OFFSET2 SIZE2
			Append

			Get MEM_SIZE asize -2

			If SIZE2 > 0
				Log FILENAME2 0 MEM_SIZE -2
			Endif

		Next B

		Goto TEMP1

	Else
		Log FILENAME OFFSET SIZE						# All other files
	Endif

Next A

