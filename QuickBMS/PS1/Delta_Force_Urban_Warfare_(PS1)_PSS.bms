# ================================================================================
# Delta Force: Urban Warfare (PS1)
# CORE.PSS audio extract
# QuickBMS script by DKDave, 2023
# ================================================================================


Get FILES Long
Get JUNK Long
Get FILE_END asize
Math ENTRY = 8
XMath DATA_START "(FILES * 12) + 8"

Log MEMORY_FILE 0 0
PutVarChr MEMORY_FILE 0x2f 0 Byte
PutVarChr MEMORY_FILE 0 0x70474156 Long

For A = 1 To FILES
	Goto ENTRY
	Get ID Long
	Get RATE Short
	Get MISC1 Short
	Get OFFSET Long
	Math OFFSET + DATA_START
	Math RATE * 10

	If A < FILES
		Goto 8 0 SEEK_CUR
		Get OFFSET2 Long
		XMath SIZE "OFFSET2 - OFFSET + DATA_START"
	Else
		XMath SIZE "FILE_END - OFFSET"
	Endif

	Log MEMORY_FILE 0 0
	PutVarChr MEMORY_FILE 0x2f 0 Byte
	PutVarChr MEMORY_FILE 0 0x70474156 Long

	Endian Big
	PutVarChr MEMORY_FILE 0x0c SIZE Long
	PutVarChr MEMORY_FILE 0x10 RATE Long
	Endian Little

	Append
	Log MEMORY_FILE OFFSET SIZE
	Append

	String FILENAME P "CORE_%A%.vag"
	Get MEM_SIZE asize -1
	Log FILENAME 0 MEM_SIZE -1

	Math ENTRY + 12
Next A

