# ================================================================================
# Spider-Man (XBox, 2002)
# XSH/XSD audio extract
# QuickBMS script by DKDave, 2020 (updated 2023)
# ================================================================================


Open FDDE "XSD" 1

Get TEMPNAME basename

Goto 0x8
Get FILES Long
Math ENTRY = 0xc

For A = 0 < FILES
	Goto ENTRY
	GetDString FILENAME 0x20
	String FILENAME P "%FILENAME%.wav"
	Get JUNK Long
	Get OFFSET Long
	Get SIZE Long

	If SIZE <> 0
		Goto 0x20 0 SEEK_CUR
		SavePos FMT_INFO
		Set MEMORY_FILE binary "RIFF\x00\x00\x00\x00WAVEfmt \x14\x00\x00\x00"

		Append
		Log MEMORY_FILE FMT_INFO 0x14
		Append

		XMath RIFF_SIZE "SIZE + 0x28"
		PutVarChr MEMORY_FILE 4 RIFF_SIZE Long
		PutVarChr MEMORY_FILE 0x26 0x40 Short
		PutVarChr MEMORY_FILE 0x28 0x61746164 Long
		PutVarChr MEMORY_FILE 0x2c SIZE Long

		Append
		Log MEMORY_FILE OFFSET SIZE 1
		Append

		Get MEM_SIZE asize -1
		Log FILENAME 0 MEM_SIZE -1
	Endif

	Math ENTRY + 0x60
Next A
