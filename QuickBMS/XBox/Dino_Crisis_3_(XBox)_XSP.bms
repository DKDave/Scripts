# ================================================================================
# Dino Crisis 3 (XBOX)
# XSP audio extract
# QuickBMS script by DKDave, 2020 (updated 2023)
# ================================================================================

# Audio is extracted as playable .wav files


Goto 0xc
Get FILES Long
Get ENTRY Long
Get DATA_START Long

For A = 0 < FILES
	Goto ENTRY
	GetDString FILENAME 0x20

	String FILENAME R ".xam" ".wav"
	String FILENAME R ".aif" ".wav"

	Get OFFSET Long
	Math OFFSET + DATA_START
	Get JUNK Long
	Get SIZE Long
	SavePos FMT_INFO

	Log MEMORY_FILE 0 0
	Set MEMORY_FILE binary "RIFF\x00\x00\x00\x00WAVEfmt \x14\x00\x00\x00"

	Append
	Log MEMORY_FILE FMT_INFO 0x14
	Append

	XMath RIFF_SIZE "SIZE + 40"
	PutVarChr MEMORY_FILE 4 RIFF_SIZE Long
	PutVarChr MEMORY_FILE 0x28 0x61746164 Long
	PutVarChr MEMORY_FILE 0x2c SIZE Long

	Append
	Log MEMORY_FILE OFFSET SIZE
	Append

	Get MEM_SIZE asize -1
	Log FILENAME 0 MEM_SIZE -1

	Math ENTRY + 0x40
Next A

